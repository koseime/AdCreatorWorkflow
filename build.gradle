apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven'
apply plugin: 'release'
apply plugin: 'gradleProtobufPlugin'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/groups/public"
        }
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'com.github.townsfolk:gradle-release:1.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
        classpath 'com.tomcawley:gradle-protobuf-plugin:0.2'
    }
}

tasks.withType(Tar) {
    compression = Compression.GZIP
}

// Use Java 8 by default
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    deployerJars
    onejar
}

repositories {
    mavenCentral()
    maven {
        url 'http://repository-kosei.forge.cloudbees.com/release/'
    }
}

// Set our project variables
project.ext {
}

dependencies {
    compile(
            'org.apache.hadoop:hadoop-common:2.2.0',
            'org.apache.hadoop:hadoop-client:2.2.0',
            'org.jdom:jdom:1.1.3',
            'com.google.protobuf:protobuf-java:2.5.0'
    )
    compile("com.twitter.elephantbird:elephant-bird-core:4.5") {
            exclude module: 'hadoop-lzo'
    }
    testCompile(
            'junit:junit:4.11',
            'org.mockito:mockito-all:1.9.5',
    )
    deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.6'
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-sources-java'
        }
    }
}

artifacts {
    archives shadowJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "dav:https://repository-kosei.forge.cloudbees.com/release/") {
            authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
        snapshotRepository(url: "dav:https://repository-kosei.forge.cloudbees.com/snapshot/") {
            authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
    }
}

createReleaseTag.dependsOn uploadArchives

release {
    tagPrefix = 'kosei-' + project.name
}

task run(type: JavaExec, dependsOn: classes) {
    classpath sourceSets.main.runtimeClasspath
    main mainClassName
    args ''
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}


protoBuf {
    protoc {
        'Mac OS X' {
            path = "/usr/local/bin/protoc"
        }
	'Linux' {
           path = "/usr/local/bin/protoc"
        }

    }

    // Optional, defaults to 'src/main/proto'
    sourceSets {
        proto {
            srcDir = 'src/main/proto'
        }
    }

    lang {
        java {
            genDir = 'build/generated-sources-java'
        }
        cpp {
            genDir = 'build/generated-sources-cpp'
        }
    }
}
