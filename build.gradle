apply plugin: 'gradle-one-jar'
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven'
apply plugin: 'release'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/groups/public"
        }
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

tasks.withType(Tar) {
    compression = Compression.GZIP
}

// Use Java 8 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    deployerJars
    onejar
    provided
}

repositories {
    mavenCentral()
    maven {
        url 'http://repository-kosei.forge.cloudbees.com/release/'
    }
}

// Set our project variables
project.ext {
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {
    compile(
            'org.apache.hadoop:hadoop-common:2.2.0',
            'org.apache.hadoop:hadoop-client:2.2.0',
            'org.jdom:jdom:1.1.3'
    )
    testCompile(
            'junit:junit:4.11',
            'org.mockito:mockito-all:1.9.5',
    )
    deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.6'
    onejar "com.kosei.services:services-common:${servicesCommonVersion}:one-jar@properties"
}

task copyOneJarProperties(type: Copy) {
    from configurations.onejar
    into "${buildDir}/one-jar"
    rename '.*.properties', 'one-jar.properties'
}

// Configure the oneJar task
task oneJar(type: OneJar, dependsOn: copyOneJarProperties) {
    mainClass = mainClassName
    additionalDir = file("${buildDir}/one-jar")
    manifest {
        attributes 'Implementation-Version': version
    }
}

artifacts {
    archives oneJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "dav:https://repository-kosei.forge.cloudbees.com/release/") {
            authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
        snapshotRepository(url: "dav:https://repository-kosei.forge.cloudbees.com/snapshot/") {
            authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
    }
}

createReleaseTag.dependsOn uploadArchives

release {
    tagPrefix = 'kosei-' + project.name
}

task run(type: JavaExec, dependsOn: classes) {
    classpath sourceSets.main.runtimeClasspath
    main mainClassName
    args ''
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}