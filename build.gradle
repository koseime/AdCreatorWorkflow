apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven'
apply plugin: 'release'
apply plugin: 'idea'
apply plugin: 'gradleProtobufPlugin'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/groups/public"
        }
    }

    dependencies {
        classpath 'com.github.townsfolk:gradle-release:1.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
        classpath 'com.tomcawley:gradle-protobuf-plugin:0.2'
    }
}

tasks.withType(Tar) {
    compression = Compression.GZIP
}

// Use Java 8 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    maven {
        url 'http://repository-kosei.forge.cloudbees.com/release/'
    }
}

// Set our project variables
project.ext {
}

dependencies {
    compile(
            'org.apache.hadoop:hadoop-common:2.2.0',
            'org.apache.hadoop:hadoop-client:2.2.0',
            'org.jdom:jdom:1.1.3',
            'com.google.protobuf:protobuf-java:2.5.0',
            'com.googlecode.json-simple:json-simple:1.1'
    )
    testCompile(
            'junit:junit:4.11',
            'org.mockito:mockito-all:1.9.5',
            'org.apache.mrunit:mrunit:1.0.0:hadoop2',
            'org.powermock:powermock-module-junit4-legacy:1.5.5',
            'org.powermock:powermock-api-mockito:1.5.5'
    )
    deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.6'
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-sources-java'
        }
    }
}

artifacts {
    archives shadowJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "dav:https://repository-kosei.forge.cloudbees.com/release/") {
            authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
        snapshotRepository(url: "dav:https://repository-kosei.forge.cloudbees.com/snapshot/") {
            authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
    }
}

createReleaseTag.dependsOn uploadArchives

release {
    tagPrefix = 'kosei-' + project.name
}

task run(type: JavaExec, dependsOn: classes) {
    classpath sourceSets.main.runtimeClasspath
    main mainClassName
    args ''
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}


idea {
    module {
        excludeDirs = [file(".gradle")]
        ["classes", "docs", "dependency-cache", "libs", "reports", "resources", "test-results", "tmp", "poms"].each {
            excludeDirs << file("$buildDir/$it")
        }
    }
}

protoBuf {
    protoc {
        'Mac OS X' {
            path = "/usr/local/bin/protoc"
        }
	'Linux' {
           path = "bin/protoc"
        }

    }

    // Optional, defaults to 'src/main/proto'
    sourceSets {
        proto {
            srcDir = 'src/main/proto'
        }
    }

    lang {
        java {
            genDir = 'build/generated-sources-java'
        }
        cpp {
            genDir = 'build/generated-sources-cpp'
        }
    }
}
